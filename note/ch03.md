# 保护模式

## 3.1 认识保护模式

Descriptor 这个宏表示的不是一段代码，而是一段 `数据结构`

```asm
; 宏 ------------------------------------------------------------------------------------------------------
;
; 描述符
; usage: Descriptor Base, Limit, Attr
;        Base:  dd
;        Limit: dd (low 20 bits available)
;        Attr:  dw (lower 4 bits of higher byte are always 0)
%macro Descriptor 3
	dw	%2 & 0FFFFh									; 段界限1，引用第二个参数去掉高16位
	dw	%1 & 0FFFFh									; 段基址1，引用第一个参数去掉高16位
	db	(%1 >> 16) & 0FFh							; 段基址2，引用第一个参数，并将高16位移动到低16位，其余的位置零
	dw	((%2 >> 8) & 0F00h) | (%3 & 0F0FFh)			; 属性1 + 段界限2 + 属性2，将一个 word 的 [0,15] 和 [24, 31] 置位参数三的相同位，[16, 23] 置位为参数而的 [24, 31]
	db	(%1 >> 24) & 0FFh							; 段基址3，保留参数1的高8位
%endmacro 											; 共 8 字节
```

在接下来的代码段 `[SECTION .gdt]` 中:

`GdtLen` 是 GDT 的长度，GdtPtr 也是个小的数据结构，它有6字节，前2个字节是GDT的界限，后4字节是 GDT 的基地址。

```asm
[SECTION .gdt]
; GDT
;                              		 段基址,    段界限，			属性
LABEL_GDT:	   		Descriptor       0,         0, 					0           	; 空描述符
LABEL_DESC_CODE32:  Descriptor       0, 		SegCode32Len - 1,	DA_C + DA_32	; 非一致代码段
LABEL_DESC_VIDEO:   Descriptor 		 0B8000h,   0ffffh, 			DA_DRW	     	; 显存首地址
; GDT 结束

GdtLen		equ	$ - LABEL_GDT	; GDT长度

; 可以把 GdtPtr 当做一个数组，基地址是索引0，界限就是数组最后一位的索引。
GdtPtr		dw	GdtLen - 1		; GDT界限
			dd	0				; GDT基地址
; GDT 选择子
SelectorCode32		equ	LABEL_DESC_CODE32 - LABEL_GDT
SelectorVideo		equ	LABEL_DESC_VIDEo - LABEL_GDT
; END of [SECTION .gdt]
```

`[BITS 16]` 明确指定了它是一个16位代码段。这段程序修改了一些 GDT 中的值，然后执行了一些不常见的指令，最后通过 jmp 指令实现一个跳转。 **在这个跳转中真正的进入了保护模式**

实际上，它将跳转到第三个 section，即 `[SECTION .s32]`，这个段是32位的，执行最后一小段代码。

```asm
[SECTION .s16]
[BITS	16]
LABEL_BEGIN:
	mov	ax, cs
	mov	ds, ax
	mov	es, ax
	mov	ss, ax
	mov	sp, 0100h

	; 初始化 32 位代码段描述符
	xor	eax, eax
	mov	ax, cs
	shl	eax, 4
	add	eax, LABEL_SEG_CODE32
	mov	word [LABEL_DESC_CODE32 + 2], ax
	shr	eax, 16
	mov	byte [LABEL_DESC_CODE32 + 4], al
	mov	byte [LABEL_DESC_CODE32 + 7], ah

	; 为加载 GDTR 作准备
	xor	eax, eax
	mov	ax, ds
	shl	eax, 4
	add	eax, LABEL_GDT		; eax <- gdt 基地址
	mov	dword [GdtPtr + 2], eax	; [GdtPtr + 2] <- gdt 基地址

	; 加载 GDTR
	lgdt	[GdtPtr]

	; 关中断
	cli

	; 打开地址线A20
	in	al, 92h
	or	al, 00000010b
	out	92h, al

	; 准备切换到保护模式
	mov	eax, cr0
	or	eax, 1
	mov	cr0, eax

	; 真正进入保护模式
	jmp	dword SelectorCode32:0	; 执行这一句会把 SelectorCode32 装入 cs,
					; 并跳转到 Code32Selector:0  处
; END of [SECTION .s16]
```

![pmtest1.bin 的执行结果](pic/pmtest1.bin 的执行结果.png)

在屏幕中出现一个红色的字母 “P”。程序的最后一部分代码中写入的两个字节是写进了显存的。

### 3.1.1 保护模式的运行环境

我们通过指令将 `org	07c00h` 写入了引导扇区，但是主引导扇区只有 512 字节。
